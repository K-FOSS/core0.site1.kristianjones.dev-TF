terraform {
  required_providers {
    #
    # Hashicorp Vault
    #
    # Docs: https://registry.terraform.io/providers/hashicorp/vault/latest/docs
    #
    nomad = {
      source = "hashicorp/nomad"
      version = "1.4.15"
    }

    #
    # GitHub Provider
    #
    # Used to fetch the latest PSQL file
    #
    # Docs: https://registry.terraform.io/providers/integrations/github/latest
    #
    github = {
      source = "integrations/github"
      version = "4.15.1"
    }

    #
    # Hashicorp Terraform HTTP Provider
    #
    # Docs: https://registry.terraform.io/providers/hashicorp/http/latest/docs
    #
    http = {
      source = "hashicorp/http"
      version = "2.1.0"
    }

    #
    # Randomness
    #
    # TODO: Find a way to best improve true randomness?
    #
    # Docs: https://registry.terraform.io/providers/hashicorp/random/latest/docs
    #
    random = {
      source = "hashicorp/random"
      version = "3.1.0"
    }
  }
}

data "github_repository" "Repo" {
  full_name = "tinkerbell/hook"
}

data "github_release" "Release" {
  repository  = data.github_repository.Repo.name
  owner       = split("/", data.github_repository.Repo.full_name)[0]
  retrieve_by = "latest"
}

data "http" "Hook" {
  url = "https://github.com/tinkerbell/hook/releases/download/${data.github_release.Release.release_tag}/hook_x86_64.tar.gz"
}

resource "nomad_volume" "Volume" {
  type                  = "csi"
  plugin_id             = "truenas"
  volume_id             = "machine-staticfiles"
  name                  = "machine-staticfiles"
  external_id           = "machine-staticfiles"

  capability {
    access_mode     = "multi-node-multi-writer"
    attachment_mode = "file-system"
  }

  deregister_on_destroy = true

  mount_options {
    fs_type = "nfs"
    mount_flags = ["nolock", "nfsvers=4"]
  }

  context = {
    node_attach_driver = "nfs"
    provisioner_driver = "freenas-nfs"
    server             = "172.16.51.21"
    share              = "/mnt/Site1.NAS1.Pool1/CSI/vols/machine-staticfiles"
  }
}

resource "nomad_job" "JobFile" {
  jobspec = templatefile("${path.module}/Job.hcl", {
    Volume = nomad_volume.Volume

    EntryScript = templatefile("${path.module}/Configs/Entry.sh", {
      HookFile = data.http.Hook.body
    })
  })
}